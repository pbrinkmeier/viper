apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'

version = '1.1'
mainClassName = 'edu.kit.ipd.pp.viper.view.MainWindow'

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-swing-junit:3.8.0'
    compile 'com.fifesoft:rsyntaxtextarea:2.6.1'
    compile 'org.apache.xmlgraphics:batik-swing:1.10'
    compile 'guru.nidi:graphviz-java:0.5.2'
    compile 'org.slf4j:slf4j-nop:1.7.25'
}

test { finalizedBy jacocoTestReport }

jacoco {
    toolVersion = "0.8.2"
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
        html.destination file("${buildDir}/reports/coverage")
    }
}

checkstyle {
    configFile = rootProject.file('checkstyle.xml')
    checkstyleTest.enabled = true
}

compileJava {
    options.compilerArgs << "-Xlint:unchecked"
}

compileTestJava {
    options.compilerArgs << "-Xlint:unchecked"
}

jar {
    manifest {
        attributes(
            'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
            'Main-Class': mainClassName
        )
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
