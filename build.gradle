apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'

version = '1.0'
mainClassName = 'edu.kit.ipd.pp.viper.view.MainWindow'

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-swing-junit:3.8.0'
    compile 'com.fifesoft:rsyntaxtextarea:2.6.1'
    compile 'org.apache.xmlgraphics:batik-swing:1.10'
    compile 'guru.nidi:graphviz-java:0.5.2'
}

test { finalizedBy jacocoTestReport }

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
        html.destination file("${buildDir}/reports/coverage")
    }
}

checkstyle {
    configFile = rootProject.file('checkstyle.xml')
}

jar {
    manifest {
        attributes(
            'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
            'Main-Class': mainClassName
        )
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
